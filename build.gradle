plugins {
    id 'application'
    alias(libs.plugins.openjfx)
    id 'bisq.post-build'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

group 'bisq'
version = file("bisq/desktop/src/main/resources/version.txt").text.trim()

application {
    project.mainClassName = 'bisq.monitor.AppChooserMain'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

jar.manifest.attributes(
        "Implementation-Title": project.name,
        "Implementation-Version": version
)

javafx {
    version = '11.0.2'
    modules = ['javafx.controls']
}

configurations.all {
    exclude group: 'io.grpc'
}

dependencies {
    implementation 'bisq:assets'
    implementation 'bisq:common'
    implementation 'bisq:core'
    implementation 'bisq:proto'
    implementation 'bisq:p2p'
    implementation 'bisq:seednode'

    implementation libs.guava
    implementation libs.guice
    implementation libs.netlayer.tor
    implementation libs.netlayer.tor.native
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    implementation libs.slf4j.api

    implementation "com.google.protobuf:protobuf-java:3.19.1"
    implementation libs.spark.core
    implementation "com.google.code.gson:gson:2.8.5"
    implementation("com.github.bisq-network:bitcoinj:42bbae9") {
        exclude(module: 'bcprov-jdk15on')
        exclude(module: 'guava')
        exclude(module: 'jsr305')
        exclude(module: 'okhttp')
        exclude(module: 'okio')
        exclude(module: 'protobuf-java')
        exclude(module: 'slf4j-api')
    }
    implementation "ch.qos.logback:logback-classic:1.1.11"
    implementation "ch.qos.logback:logback-core:1.1.11"

    testAnnotationProcessor libs.lombok
    testCompileOnly libs.lombok
    testImplementation libs.junit.jupiter
}

clean.doFirst {
    // Remove the project root level 'bisq-*' scripts and 'lib' dir generated at the end of the assemble task.
    delete fileTree(dir: projectDir, include: ['bisq-monitor', 'bisq-monitor.bat']), 'lib'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
